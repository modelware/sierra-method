vocabulary <http://modelware.io/sierra/function#> as function {

	extends <http://modelware.io/sierra/base#> as base

	extends <http://modelware.io/sierra/capability#> as capability

	extends <http://modelware.io/sierra/component#> as component

	aspect ExchangeElement

	relation entity Exchange [
		from ExchangeElement
		to ExchangeElement
		forward sendsTo
		reverse receivesFrom
	] < base:ContainableElement [
		restricts all base:isContainedIn to Chain
	]

	concept Chain < base:ContainableElement [
		restricts all base:contains to Exchange
	]

	concept Function < ExchangeElement

	concept Port < ExchangeElement

	relation hasPort [
		from Function
		to Port
		reverse isPortOf
		inverse functional
	]

	concept Input < Port

	relation hasInput [
		from Function
		to Input
		reverse isInputOf
	] < hasPort

	concept Output < Port

	relation hasOutput [
		from Function
		to Output
		reverse isOutputOf
	] < hasPort

	concept Item

	relation conveys [
		from Exchange
		to Item
		reverse isConveyedBy
		functional
	]

	relation implements [
		from Chain
		to capability:Capability
		reverse isImplementedBy
		functional
		inverse functional
	]

	aspect PerformingElement

	relation performs [
		from PerformingElement
		to Function
		reverse isPerformedBy
	]

	ref concept capability:Actor < PerformingElement

	ref concept capability:Entity < PerformingElement

	ref concept component:Component < PerformingElement
}